# $Id: configure.in,v 1.6 2004/10/21 00:59:46 dhanks Exp $
#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.5)
AC_INIT(sudosh, [1.6.3], [dhanks@gmail.com])
AC_CONFIG_SRCDIR([src/])
AC_CANONICAL_SYSTEM
AC_CONFIG_HEADER([config.h])
AM_INIT_AUTOMAKE(sudosh, 1.6.3)
AC_PROG_CC
AC_PATH_PROG(PERL, perl)

logdir=
AC_MSG_CHECKING(for logdir)
AC_ARG_WITH(logdir,
[  --with-logdir=PATH      specify path where logfiles will be written],
[
  if test "x$withval" != "x" ; then
    logdir=$withval
  fi
])
if test "x$logdir" = "x" ; then
  case "$host_os" in
    *linux*)
      logdir="/var/log/sudosh"
      ;;
    *solaris*)
      logdir="/var/adm/sudosh"
      ;;
    *hp*)
      logdir="/var/adm/sudosh"
      ;;
    *aix*)
      logdir="/var/adm/sudosh"
      ;;
    *)
      logdir="/tmp/sudosh"
      ;;
  esac
fi
AC_MSG_RESULT($logdir)
AC_DEFINE_UNQUOTED(LOGDIR,"$logdir",[directory for session output])

filedelimit=
AC_MSG_CHECKING(for filedelimit)
AC_ARG_WITH(filedelimit,
[  --with-filedelimit=CHAR specify character to delimit files],
[
  if test "x$withval" != "x" ; then
    filedelimit=`echo $withval|cut -b1`
  fi
])
if test "x$filedelimit" = "x" ; then
    filedelimit="-"
fi
AC_MSG_RESULT($filedelmit)
AC_DEFINE_UNQUOTED(FILEDELIMIT,"$filedelimit",[character to be used to delimit files])

defshell=
AC_MSG_CHECKING(for default shell)
AC_ARG_WITH(defshell,
[  --with-defshell=PATH    specify default shell if to be used as a login shell.],
[
  if test "x$withval" != "x" ; then
    defshell=$withval
  fi
])
if test "x$defshell" = "x" ; then
    defshell="/bin/sh"
fi
AC_MSG_RESULT($defshell)
AC_DEFINE_UNQUOTED(DEFSHELL,"$defshell",[default shell if to be used as a login shell.])

# Check for /etc/shells
etcshells=
AC_MSG_CHECKING(for /etc/shells)
if test -f /etc/shells; then
  AC_MSG_RESULT(found)
  AC_DEFINE(HAS_ETC_SHELLS, 1,[have /etc/shells])
else
  AC_MSG_RESULT(not found)
  AC_DEFINE(HAS_ETC_SHELLS, 0,[does not have /etc/shells])
fi


# Checks for libraries.
if test "x$prefix" = "xNONE" ; then
    prefix="/usr/local"
    prefix_nondefault="NONE"
fi

# Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([pty.h fcntl.h sys/termio.h limits.h stdlib.h string.h sys/stat.h sys/param.h syslog.h termios.h stropts.h unistd.h time.h sys/time.h usersec.h])
AC_CHECK_FUNCS(openpty, openpty=1, openpty=0)
AC_CHECK_FUNCS(getusershell gettimeofday)
AC_CHECK_FUNCS(mkdir rmdir)
AC_CHECK_FUNCS(setpenv)

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_HEADER_TIME

# Checks for library functions.
AC_FUNC_FORK
AC_PROG_GCC_TRADITIONAL
AC_FUNC_SELECT_ARGTYPES
AC_TYPE_SIGNAL
AC_CHECK_FUNCS([memchr memmove memset select strrchr])

VLOGDIR=$logdir
VFILEDELIMIT=$filedelimit
AC_SUBST([VLOGDIR])
AC_SUBST([VFILEDELIMIT])

AC_CONFIG_FILES([Makefile
                 src/Makefile
		 src/sudosh-replay], [chmod +x src/sudosh-replay])
AC_OUTPUT

temp_mandir=`eval echo $mandir` ; temp_mandir=`eval echo $temp_mandir`

echo ""
echo "Configuration summary:"
echo "======================"
echo ""
echo "  Host type................: $target"
echo "  Perl.....................: $PERL"
echo "  CC.......................: $CC"
echo "  CFLAGS...................: $CFLAGS"
echo ""
echo "  Package..................: $PACKAGE_NAME"
echo "  Version..................: $VERSION"
echo "  Log directory............: $logdir"
echo "  File delimiter...........: $filedelimit"
echo ""
echo "  Installation prefix......: $prefix"
echo "  Man directory............: $temp_mandir"
echo "  Default shell............: $defshell"
echo ""
echo "If this summary looks good, type 'make' or 'gmake' to compile"
echo "then 'make install' or 'gmake install' to install the programs to"
echo "the above installation prefix."
